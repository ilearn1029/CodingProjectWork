; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "InventorySystem"
#define MyAppVersion "1.0"
#define MyAppPublisher "My Company, Inc."
#define MyAppURL "http://www.example.com/"
#define MyAppExeName "InventorySystem.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{04AC698B-DA0C-47D1-B224-3F6CAA505A48}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DisableProgramGroupPage=yes
OutputBaseFilename=InventorySystemSetup
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\Output\Firebird25Install\Firebird-2.5.4.26856_0_Win32.exe"; DestDir: {tmp}; AfterInstall: RunApp(ExpandConstant('{tmp}\Firebird-2.5.4.26856_0_Win32.exe'), ' /NOCPL /SILENT /SP- /NORESTART'); 
Source: "..\Output\Firebird25Install\security2.fdb"; DestDir: {code:FirebirdLocation}; check: foundFirebirdLocation; Flags: uninsneveruninstall; 
Source: "..\Output\fbclient.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Output\INVENTORYDB"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Output\InventorySystem.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Output\InventorySystem.ini"; DestDir: "{app}"; Flags: ignoreversion

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{commonprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
function FirebirdLocation(AParam: string): String;
var
  LFBLocation: string;
begin
  result := '';  
  if RegQueryStringValue(HKLM, 'SOFTWARE\Firebird Project\Firebird Server\Instances', 'DefaultInstance', LFBLocation) then
    result := LFBLocation;
  
end;

function foundFirebirdLocation: boolean;
begin
  Result := FirebirdLocation('') <> '';
end;

procedure RunApp(App: string; Param: string);
Var
  ResultCode : Integer;
begin
  if not Exec(App, Param, '', SW_SHOW, ewWaitUntilTerminated, ResultCode) then
  begin
    msgbox('Install ' + App + ' ' + Param + ' failed ' + ' error code ' + intToStr(ResultCode)
       + #13#10 + 'Setup will be terminated.', mbInformation, MB_OK);
    PostMessage (WizardForm.Handle, $0010, 0, 0); 
  end
end;

